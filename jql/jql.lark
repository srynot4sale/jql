action: "CREATE" data+                  -> create
      | "CREATE" content data*          -> create
      | "HINTS" ( tag "/"? | fact )?    -> hints
      | "CHANGESETS"                    -> changesets
      | "REPLICATE"                     -> replicate
      | match "ARCHIVE"                 -> archive
      | match "SET" data+               -> set
      | match "SET" content             -> set
      | match "DEL" data+               -> del
      | id                              -> get
      | data+                           -> list
      | content data*                   -> list
      | id? "HISTORY"                   -> history

?data: tag
      | fact
      | value

?match: id
      | data+

?content: quotedtext
        | simpletext

id                  : "@" ID
value               : fact "=" (/[\S]+/|quotedtext)
fact                : tag "/" PROP
tag                 : "#" TAG
quotedtext          : /\[\[\[(.*?)\]\]\]/s
simpletext          : /(?<![#@\S])(?!\[\[\[)(?!HINTS)(?!CREATE)((?![#@])[^\n ]+ *)+/s

ID      : HEXDIGIT+
HEXDIGIT: "a".."f"|DIGIT
TAG     : "_"? (LCASE_LETTER) (LCASE_LETTER|DIGIT)*
PROP    : (LCASE_LETTER) ("_"|LCASE_LETTER|DIGIT)*
%import common.LCASE_LETTER
%import common.DIGIT
%import common.WS
%ignore WS
